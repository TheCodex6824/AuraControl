buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "${mcversion}-${version}"
group = "thecodex6824.auracontrol" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AuraControl"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mcversion}-${forgeversion}"
    runDir = "run"
    useDepAts = true
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    replace '@APIVERSION@', project.apiversion
    replaceIn 'AuraControlAPI.java'
    
    replace '@VERSION@', project.version
    replaceIn 'AuraControl.java'
}

dependencies {
    deobfCompile 'curse.maven:thaumcraft-223628:2629023'
    
    runtime 'curse.maven:jei-238222:2995897'
    runtime 'curse.maven:thaumic-jei-285492:2705304'
	runtime 'curse.maven:baubles-227083:2518667'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "apiversion", project.apiversion
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'forgeversion':project.forgeversion, 'forgeminversion':project.forgeminversion
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'thecodex6824/auracontrol/api/**'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

artifacts {
    archives apiJar
    archives javadocJar
    archives sourcesJar
}
